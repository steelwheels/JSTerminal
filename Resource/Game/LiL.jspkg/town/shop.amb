top: VBox {
	global: Dictionary {
		storage:	"main"
		path:		"data.global.dict"
	}
	having_weapons: Table {
		storage:	"main"
		path:		"data.having.weapons"
	}
	weapons: Table {
		storage:	"main"
		path:		"data.list.weapons"
	}

	title: ImageView {
		name: "shop_title"
	}

    weapon_table: TableView {
		hasHeader:    true
		fieldNames: [
			{field:"count",		title:"Count"},
			{field:"name",     	title:"Name"},
			{field:"price",		title:"Price"}
		]
		init: Init %{
			self.dataTable = top.weapons.table() ;
			self.reload() ;
		%}
		filter: Event(rec) %{
			return (rec.price > 0) ;
		%}
		isEnable: Func(self, record) %{
			return top.global.value("money") >= record.price ;
		%}
		virtualFields: Object {
			count:	Func(self, record) %{
					let hrecs = top.having_weapons.search(
						"weapon_id", record.weapon_id) ;
					if(hrecs.length > 0){
						let hrec = hrecs[0] ;
						if(isNumber(hrec.count)){
							return hrec.count ;
						}
					}
					return 0 ;
			%}
			name: Func(self, record) %{
					let hrecs = top.having_weapons.search(
						"weapon_id", record.weapon_id) ;
					if(hrecs.length > 0){
						let hrec = hrecs[0] ;
						if(hrec.isKnown){
							return record.long_name ;
						}
					}
					return record.short_name ;
			%}
		}
    }

	ret_button: Button {
		title:  "Return to town"
		pressed: Event() %{
			leaveView(0) ;
		%}
	}
}

