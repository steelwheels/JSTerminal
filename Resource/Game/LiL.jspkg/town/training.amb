top: VBox {
    members: Storage {
        name: String "main"
        path: String "town.members"
    }

    title: Image {
        name: String "training_title"
    }

    partyTitle: Label {
	text: String "Party members"
    }
    partyTable: TableView {
	hasHeader:       Bool true
	isSelectable:    Bool true
	visibleRowCount: Int  6
	fieldNames: Array [
                {field:"name",          title:"Name"},
                {field:"level",         title:"Level"},
                {field:"hitPoint",      title:"HP"},
                {field:"magicPoint",    title:"MP"}
        ]
	init: Init %{
		self.dataTable = top.members.table ;
		self.reload() ;
	%}
	filter: Event(record) %{
		let char = new Character.Character(record) ;
		return char.isPartyMember() ;
	%}
    }
    editParty: HBox {
	    join_button: Button {
		title:  String "Join party"
		isEnabled: Bool Listner(didsel: top.memberTable.didSelected) %{
			return didsel ;
		%}
		pressed: Event() %{
			console.print("Join party\n") ;
			let recs = top.memberTable.selectedRecords() ;
			for(rec of recs){
				let char = new Character.Character(rec) ;
				char.pid = top.partyTable.rowCount ;
				top.partyTable.reload() ;
				top.memberTable.reload() ;
			}
		%}
	    }
	    leave_button: Button {
		title:  String "Leave party"
		isEnabled: Bool Listner(didsel: top.partyTable.didSelected) %{
			return didsel ;
		%}
		pressed: Event() %{
			console.print("Leave party\n") ;
			let recs = top.partyTable.selectedRecords() ;
			for(rec of recs){
				let char = new Character.Character(rec) ;
				char.pid = 0 ; // No pid
				top.partyTable.reload() ;
				top.memberTable.reload() ;
			}
		%}
	    }
    }

    memberTitle: Label {
	text: String "All members"
    }
    memberTable: TableView {
	hasHeader:    Bool true
	isSelectable: Bool true
	fieldNames: Array [
                {field:"name",          title:"Name"},
                {field:"level",         title:"Level"},
                {field:"hitPoint",      title:"HP"},
                {field:"magicPoint",    title:"MP"}
        ]
	init: Init %{
		self.dataTable = top.members.table ;
		self.reload() ;
	%}
	filter: Event(record) %{
		let char = new Character.Character(record) ;
		return !char.isPartyMember() ;
	%}
    }
    change_member: HBox {
	    add_button: Button {
		title:  String "Add member"
		pressed: Event() %{
			let record = enterView("gym", null) ;
			if(isRecord(record)){
				top.members.table.append(record) ;
				top.memberTable.reload() ;
			}
		%}
	    }
	    del_button: Button {
		title:  String "Delete member"
		isEnabled: Bool Listner(didsel: top.memberTable.didSelected) %{
			return didsel ;
		%}
		pressed: Event() %{
                        top.memberTable.removeSelectedRows() ;
		%}
	    }
    }
    quit_button: Button {
        title:  String "Quit"
        pressed: Event() %{
		top.members.table.save() ;
		leaveView(0) ;
        %}
    }
}

