top: VBox {
    members: Storage {
        name: "main"
        path: "town.members"
    }

    title: ImageView {
        name: "tavern_title"
    }

    partyTitle: Label {
		text: "Party members"
    }
    partyTable: TableView {
		hasHeader:       true
		isSelectable:    true
		visibleRowCount: 6
		fieldNames: [
					{field:"name",          title:"Name"},
					{field:"level",         title:"Level"},
					{field:"hitPoint",      title:"HP"},
					{field:"magicPoint",    title:"MP"}
			]
		init: Init %{
			self.dataTable = top.members.table ;
			self.reload() ;
		%}
		filter: Func(self, rec) %{
			console.log("filter: " + rec.name + " -> " + rec.pid) ;
			return rec.pid > 0 ;
		%}
    }
    editParty: HBox {
	    join_button: Button {
		title:  "Join party"
		isEnabled: Listner(didsel: top.memberTable.didSelected) %{
			return didsel ;
		%}
		pressed: Event() %{
			console.print("Join party\n") ;
			let recs = top.memberTable.selectedRecords() ;
			for(rec of recs){
				/* Assign pid */
				rec.pid = top.partyTable.rowCount ;
				console.log("assign: " + rec.name + " -> " + rec.pid) ;
			}
			top.partyTable.reload() ;
			top.memberTable.reload() ;
		%}
	    }
	    leave_button: Button {
		title:  "Leave party"
		isEnabled: Listner(didsel: top.partyTable.didSelected) %{
			return didsel ;
		%}
		pressed: Event() %{
			console.print("Leave party\n") ;
			let recs = top.partyTable.selectedRecords() ;
			for(rec of recs){
				let char = new Character.Character(rec) ;
				char.pid = 0 ; // No pid
				top.partyTable.reload() ;
				top.memberTable.reload() ;
			}
		%}
	    }
    }

    memberTitle: Label {
	text: "All members"
    }
    memberTable: TableView {
	hasHeader:    true
	isSelectable: true
	fieldNames: [
                {field:"name",          title:"Name"},
                {field:"level",         title:"Level"},
                {field:"hitPoint",      title:"HP"},
                {field:"magicPoint",    title:"MP"}
        ]
	init: Init %{
		self.dataTable = top.members.table ;
		self.reload() ;
	%}
	filter: Event(record) %{
		let char = new Character.Character(record) ;
		return !char.isPartyMember() ;
	%}
    }
    change_member: HBox {
	    add_button: Button {
		title:  "Add member"
		pressed: Event() %{
			let record = enterView("gym", null) ;
			if(isRecord(record)){
				top.members.table.append(record) ;
				top.memberTable.reload() ;
			}
		%}
	    }
	    del_button: Button {
		title:  "Delete member"
		isEnabled: Listner(didsel: top.memberTable.didSelected) %{
			return didsel ;
		%}
		pressed: Event() %{
                        top.memberTable.removeSelectedRows() ;
		%}
	    }
    }
    quit_button: Button {
        title:  "Quit"
        pressed: Event() %{
		top.members.table.save() ;
		leaveView(0) ;
        %}
    }
}

